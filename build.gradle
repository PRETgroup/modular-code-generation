version '0.5.9'

buildscript {
  ext.kotlin_version = '1.3.72'
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
  }
}

apply plugin: 'kotlin'
apply plugin: 'application'
apply plugin: 'jacoco'

mainClassName = 'me.nallen.modularcodegeneration.AppKt'

defaultTasks 'run'

repositories {
  mavenCentral()
  jcenter()
}

dependencies {
  compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
  compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
  testCompile 'junit:junit:4.13'
  testCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
  testCompile 'io.kotlintest:kotlintest:2.0.7'
  compile "com.fasterxml.jackson.core:jackson-databind:2.11.0"
  compile "com.fasterxml.jackson.module:jackson-module-kotlin:2.11.0"
  compile "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.11.0"
  compile "com.fasterxml.jackson.dataformat:jackson-dataformat-xml:2.11.0"
  compile "com.xenomachina:kotlin-argparser:2.0.7"
  compile "com.hubspot.jinjava:jinjava:2.5.4"
  compile "org.slf4j:slf4j-simple:1.7.30"
}

jacocoTestReport {
  reports {
    xml.enabled true
    html.enabled true
  }
}

test {
  testLogging {
    events = ["standard_out", "passed", "failed", "skipped"]
  }
}

def generatedVersionDir = "${buildDir}/generated-version"

sourceSets {
    main {
        output.dir(generatedVersionDir, builtBy: 'generateVersionProperties')
    }
}

task generateVersionProperties {
    doLast {
        def propertiesFile = file "$generatedVersionDir/version.properties"
        propertiesFile.parentFile.mkdirs()
        def properties = new Properties()
        properties.setProperty("version", rootProject.version.toString())
        propertiesFile.withWriter { properties.store(it, null) }
    }
}
processResources.dependsOn generateVersionProperties

jar {
  manifest {
    attributes 'Main-Class': 'me.nallen.modularcodegeneration.CliKt'
  }

  from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
}
